import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.TestFrameworkType
import org.jetbrains.intellij.platform.gradle.models.ProductRelease

buildscript {
	repositories {
		maven {
			url = uri("https://plugins.gradle.org/m2/")
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
		maven {
			url "https://www.jetbrains.com/intellij-repository/snapshots"
		}
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
}

plugins {
	id("com.liferay.source.formatter") version "latest.release"
	id("de.undercouch.download") version "5.1.0"
	id("java-library")
	id("nu.studer.credentials") version "2.1"
	id("org.jetbrains.intellij.platform") version "2.6.0"
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:unchecked"
	options.deprecation = true
}

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java"]
		}
		resources {
			srcDirs = ["src/main/resources"]
		}
	}
}

tasks.register('dockerImageBuild', Exec)
tasks.register('dockerPublishRelease', Exec)
tasks.register('downloadBlade392', Download)
tasks.register('downloadBladeLatest', Download)
tasks.register('downloadDtds', Download)
tasks.register('downloadTlds', Download)
tasks.register('downloadXsds', Download)
tasks.register('verifyBlade392', Verify)
tasks.register('verifyBladeLatest', Verify)

String userHome = System.getProperty("user.home")

String gradleHome = System.getProperty("gradle.user.home") ?: System.getenv("GRADLE_USER_HOME") ?: new File(userHome, ".gradle").absolutePath

File gradleCredentialsProperties = new File(gradleHome, "gradle.credentials.properties")

assemble {
	dependsOn buildPlugin
}

buildScan {
	termsOfServiceUrl = "https://gradle.com/terms-of-service"
	termsOfServiceAgree = "yes"
}

check.dependsOn checkSourceFormatting
checkSourceFormatting {
	maxLineLength = 120
}

configurations.configureEach {
	exclude group: 'org.slf4j', module: 'slf4j-api'
}

dependencies {
	api files( "libs/gradle-tooling.jar")
	api group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "6.4.0"
	api group: "com.liferay", name: "com.liferay.release.util", version: "1.0.0"
	api group: "commons-configuration", name: "commons-configuration", version: "1.10"
	api group: "commons-lang", name: "commons-lang", version: "2.6"
	api group: "org.apache.ant", name: "ant", version: "1.10.13"
	api group: "org.dom4j", name: "dom4j", version: "2.1.4"
	api group: "org.json", name: "json", version: "20230227"
	api group: "org.osgi", name: "org.osgi.core", version: "6.0.0"

	testImplementation group: "junit", name: "junit", version: "4.13.2"
	testImplementation group: "org.opentest4j", name: "opentest4j", version: "1.3.0"

	intellijPlatform {
		// Community
		bundledPlugin "com.intellij.java-i18n"
		bundledPlugin "com.intellij.properties"
		bundledPlugin "JUnit"
		bundledPlugin "org.intellij.groovy"
		bundledPlugin "org.jetbrains.idea.maven"
		bundledPlugin "org.jetbrains.plugins.gradle"
		bundledPlugin 'com.intellij.java'

		// Ultimate
		bundledPlugin "com.intellij.database"
		bundledPlugin "com.intellij.javaee"
		bundledPlugin "com.intellij.javaee.web"
		bundledPlugin "com.intellij.jsp"
		bundledPlugin "com.intellij.persistence"
		bundledPlugin "com.intellij.plugins.webcomponents"
		bundledPlugin "JavaScript"
		bundledPlugin "JSIntentionPowerPack"
		bundledPlugin "org.jetbrains.plugins.sass"

		intellijIdeaUltimate '2025.2'

		testFramework TestFrameworkType.Bundled.INSTANCE
	}
}

dockerImageBuild {
	List<String> args = ['docker', 'build', '--tag', 'liferay-intellij-plugin-build', '.']

	commandLine args
}

dockerPublishRelease {
	dependsOn dockerImageBuild

	buildSearchableOptions.enabled = false

	List<String> args = ['docker', 'run', '--rm', '--init', '-v', "${userHome}/.gradle/wrapper:/root/.gradle/wrapper"]

	if (gradleCredentialsProperties.exists()) {
		args.addAll(['-v', "${userHome}/.gradle/gradle.credentials.properties:/root/.gradle/gradle.encrypted.properties"])
	}

	args.addAll(['liferay-intellij-plugin-build'])

	commandLine args
}

downloadBlade392 {
	src blade392DownloadURL
	dest file("${bladeBuildOutputDir}/blade-3.9.2.jar")
	onlyIfModified true
}

buildPlugin{
	finalizedBy(":checkSourceFormatting")
}

downloadBladeLatest {
	src bladeLatestDownloadURL
	dest file("${bladeBuildOutputDir}/blade-latest.jar")
	onlyIfModified true
}

downloadDtds {
	def target = file("src/main/resources/definitions/dtd")

	target.mkdirs()

	src([
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_4_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_0_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_1_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_2_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_3_0.dtd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_4_0.dtd"
	])

	dest target
	overwrite false
}

downloadTlds {
	def target = file("src/main/resources/definitions/tld")

	target.mkdirs()

	src([
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/util-taglib/src/META-INF/liferay-aui.tld",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/util-taglib/src/META-INF/liferay-ui.tld"
	])

	dest target
	overwrite false
}

downloadXsds {
	def target = file("src/main/resources/definitions/xsd")

	target.mkdirs()

	src([
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_0_0.xsd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_1_0.xsd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_2_0.xsd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_3_0.xsd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_4_0.xsd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/portlet-app_2_0.xsd",
		"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/portlet-app_3_0.xsd"
	])

	dest target
	overwrite false
}

formatSource {
	maxLineLength = 120
}

def marketplaceKey = credentials.marketplaceKey ?: ""

intellijPlatform {
	pluginConfiguration {
		ideaVersion {
			sinceBuild = "251"
		}

		name = "liferay-intellij-plugin"
	}

	pluginVerification {
		freeArgs = ["-mute", "TemplateWordInPluginId", "-mute", "TemplateWordInPluginName"]

		ides {
			select {
				it.types = [IntelliJPlatformType.IntellijIdeaCommunity, IntelliJPlatformType.IntellijIdeaUltimate]
				it.channels = [ProductRelease.Channel.RELEASE]
				it.sinceBuild = "251"
				it.untilBuild = "252.*"
			}
		}

		ignoredProblemsFile = file("ignoredProblems.txt")
	}

	publishing {
		token = marketplaceKey
	}
}

processResources {

	dependsOn verifyBlade392
	dependsOn verifyBladeLatest
	dependsOn downloadDtds
	dependsOn downloadTlds
	dependsOn downloadXsds

	from('src/main/java') {
		include '**/init.gradle'
	}
	into("resources/libs/")
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
	mavenCentral()

	intellijPlatform {
		defaultRepositories()
	}
}

runIde {
	jvmArgs = [ "-Xmx2048m", "--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED" ]
}

test {
	testLogging {
		exceptionFormat = "full"
	}
	scanForTestClasses false
	include "**/*Test.class"
}

verifyBlade392 {
	dependsOn downloadBlade392
	src file("${bladeBuildOutputDir}/blade-3.9.2.jar")
	algorithm "MD5"
	checksum blade392MD5
}

verifyBladeLatest {
	dependsOn downloadBladeLatest
	src file("${bladeBuildOutputDir}/blade-latest.jar")
	algorithm "MD5"
	checksum bladeLatestMD5
}

version = "3.0.1"