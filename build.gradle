buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
		classpath group: "nu.studer", name: "gradle-credentials-plugin", version: "2.1"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
}

plugins {
	id "de.undercouch.download" version "4.0.0"
	id "org.jetbrains.intellij" version "0.7.2"
}

apply plugin: "com.liferay.source.formatter"
apply plugin: "java"
apply plugin: "nu.studer.credentials"

tasks.withType(JavaCompile) {
	options.compilerArgs << "-Xlint:unchecked"
	options.deprecation = true
}

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java"]
		}
		resources {
			srcDirs = ["src/main/resources"]
		}
	}
}

task dockerImageBuild(type:Exec)
task dockerPublishRelease(type:Exec)
task downloadBlade392(type: Download)
task downloadBladeLatest(type: Download)
task downloadDtds(type: Download)
task downloadTlds(type: Download)
task downloadXsds(type: Download)
task verifyBlade392(type: Verify)
task verifyBladeLatest(type: Verify)

String userHome = System.getProperty("user.home")

String gradleHome = System.getProperty("gradle.user.home") ?: System.getenv("GRADLE_USER_HOME") ?: new File(userHome, ".gradle").absolutePath

File gradleCredentialsProperties = new File(gradleHome, "gradle.credentials.properties")

buildScan {
	termsOfServiceUrl = "https://gradle.com/terms-of-service"
	termsOfServiceAgree = "yes"
}

check.dependsOn checkSourceFormatting

checkSourceFormatting {
	maxLineLength = 120
}

configurations.all {
	exclude group: 'org.slf4j', module: 'slf4j-api'
}

dependencies {
	compile files( "libs/gradle-tooling.jar")
	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.4.0"
	compile group: "com.github.docker-java", name: "docker-java", version: "3.1.5"
	compile group: "commons-configuration", name: "commons-configuration", version: "1.10"
	compile group: "commons-lang", name: "commons-lang", version: "2.6"
	compile group: "org.apache.ant", name: "ant", version: "1.10.1"
	compile group: "org.dom4j", name: "dom4j", version: "2.1.0"
	compile group: "org.osgi", name: "org.osgi.core", version: "6.0.0"

	testCompile group: "junit", name: "junit", version: "4.12"
}

dockerImageBuild {
	List<String> args = ['docker', 'build', '--tag', 'liferay-intellij-plugin-build', '.']

	commandLine args
}

dockerPublishRelease {
	dependsOn dockerImageBuild

	buildSearchableOptions.enabled = false

	List<String> args = ['docker', 'run', '--rm', '--init', '-v', "${userHome}/.gradle/wrapper:/root/.gradle/wrapper"]

	if (gradleCredentialsProperties.exists()) {
		args.addAll(['-v', "${userHome}/.gradle/gradle.credentials.properties:/root/.gradle/gradle.encrypted.properties"])
	}

	args.addAll(['liferay-intellij-plugin-build'])

	commandLine args
}

downloadBlade392 {
	src blade392DownloadURL
	dest file("${bladeBuildOutputDir}/blade-3.9.2.jar")
	onlyIfModified true
}

downloadBladeLatest {
	src bladeLatestDownloadURL
	dest file("${bladeBuildOutputDir}/blade-latest.jar")
	onlyIfModified true
}

downloadDtds {
	def target = file("src/main/resources/definitions/dtd")

	target.mkdirs()

	src([
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_3_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_0_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_1_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_2_0.dtd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_3_0.dtd"
		])

	dest target
	overwrite false
}

downloadTlds {
	def target = file("src/main/resources/definitions/tld")

	target.mkdirs()

	src([
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/util-taglib/src/META-INF/liferay-aui.tld",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/util-taglib/src/META-INF/liferay-ui.tld"
		])

	dest target
	overwrite false
}

downloadXsds {
	def target = file("src/main/resources/definitions/xsd")

	target.mkdirs()

	src([
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_0_0.xsd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_1_0.xsd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_2_0.xsd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_3_0.xsd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/portlet-app_2_0.xsd",
			"https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/portlet-app_3_0.xsd"
		])

	dest target
	overwrite false
}

formatSource {
	maxLineLength = 120
}

intellij {
	plugins "java", "CSS", "DatabaseTools", "gradle", "Groovy", "java-i18n", "JavaEE", "Web", "JavaScriptLanguage", "jsp", "junit", "maven", "PersistenceSupport", "properties", "sass"
	pluginName "liferay-intellij-plugin"
	version "IU-2021.1"
}

patchPluginXml {
	sinceBuild "211"
	untilBuild ""
}

processResources {

	dependsOn verifyBlade392
	dependsOn verifyBladeLatest
	dependsOn downloadDtds
	dependsOn downloadTlds
	dependsOn downloadXsds

	from('src/main/java') {
		include '**/init.gradle'
	}
	into("resources/libs/")
}

def marketplaceKey = credentials.marketplaceKey ?: ""

publishPlugin {
	token marketplaceKey
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

runIde {
	jvmArgs = [ "-Xmx2048m", "--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED" ]
}

test {
	testLogging {
		exceptionFormat = "full"
	}
}

verifyBlade392 {
	dependsOn downloadBlade392
	src file("${bladeBuildOutputDir}/blade-3.9.2.jar")
	algorithm "MD5"
	checksum blade392MD5
}

verifyBladeLatest {
	dependsOn downloadBladeLatest
	src file("${bladeBuildOutputDir}/blade-latest.jar")
	algorithm "MD5"
	checksum bladeLatestMD5
}

version = "1.9.2"