buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
}

plugins {
	id "de.undercouch.download" version "3.4.2"
	id "org.jetbrains.intellij" version "0.3.1"
}

apply plugin: 'com.liferay.source.formatter'
apply plugin: 'java'

task downloadBlade(type: Download)
task downloadLiferayDefinitions(type: Download)
task verifyBlade(type: Verify)

check.dependsOn checkSourceFormatting

checkSourceFormatting {
	maxLineLength = 120
}

dependencies {
	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.4.0"
	compile group: "commons-configuration", name: "commons-configuration", version: "1.10"
	compile group: "org.apache.ant", name: "ant", version: "1.10.1"
	compile group: "org.dom4j", name: "dom4j", version: "2.1.0"
}

downloadBlade {
	src bladeLatestDownloadURL
	dest 'build/resources/main/libs/blade.jar'
	onlyIfModified true
}

downloadLiferayDefinitions {
	def target = file("build/resources/main/definitions")

	target.mkdirs()

	src([
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-display_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-friendly-url-routes_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-hook_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-layout-templates_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-look-and-feel_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-package_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-plugin-repository_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-portlet-app_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-resource-action-mapping_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-service-builder_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-social_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-theme-loader_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_0_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-user-notification-definitions_7_1_0.dtd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_0_0.xsd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/liferay-workflow-definition_7_1_0.xsd',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/definitions/portlet-app_2_0.xsd'
		])

	dest target
	overwrite false
}

task downloadLiferayTaglibDefinitions(type: Download){
	def target = file("build/resources/main/tld")

	target.mkdirs()

	src([
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/util-taglib/src/META-INF/liferay-aui.tld',
			'https://raw.githubusercontent.com/liferay/liferay-portal/master/util-taglib/src/META-INF/liferay-ui.tld'
		])

	dest target
	overwrite false
}

formatSource {
	maxLineLength = 120
}

intellij {
	plugins 'gradle', 'maven', 'terminal', 'JavaEE', 'java-i18n', 'properties'
	version "IU-2018.1.4"
}

patchPluginXml {
	sinceBuild '172'
	untilBuild ''
}

processResources {
	dependsOn verifyBlade, downloadLiferayDefinitions, downloadLiferayTaglibDefinitions
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

verifyBlade {
	dependsOn downloadBlade
	src 'build/resources/main/libs/blade.jar'
	algorithm 'MD5'
	checksum bladeLatestMD5
}

version = '1.1.0'